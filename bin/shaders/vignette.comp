#version 460

uniform sampler2D source;
layout(rgba8, binding = 1) writeonly uniform image2D destination;

uniform float radius;

/* Gradient noise from Jorge Jimenez's presentation: */
/* http://www.iryoku.com/next-generation-post-processing-in-call-of-duty-advanced-warfare */
float gradientNoise(in vec2 uv)
{
    return fract(52.9829189 * fract(dot(uv, vec2(0.06711056, 0.00583715))));
}

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
    ivec2 texel = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = vec2(texel) / vec2(textureSize(source, 0));

    vec3 vignette = mix(1.f, 0f, distance(uv, vec2(.5f)) * radius) * texture(source, uv).xyz;

    // dithering pattern using gradient noise to avoid color banding
    vignette += (1.0 / 255.0) * gradientNoise(vec2(texel)) - (0.5 / 255.0);
    imageStore(destination, texel, vec4(vignette, 1));
}
