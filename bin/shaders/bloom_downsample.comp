#version 460

uniform sampler2D source;
layout(rgba16f, binding = 1) writeonly uniform image2D result;

uniform int mipLevel;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
    ivec2 texel = ivec2(gl_GlobalInvocationID.xy);
    vec2 size = vec2(imageSize(result));
    if(texel.x > size.x || texel.y > size.y)
        return;

    vec2 uv = texel / size;
    vec2 texelSize = 1.f / vec2(textureSize(source, mipLevel - 1));
    vec2 texSize2 = texelSize * 2.f;

    // Jimenez's sampling technique from https://www.iryoku.com/next-generation-post-processing-in-call-of-duty-advanced-warfare/
    // overall we sample a grid in the form of, with X the current pixel
    // | E |   | F |   | G |
    // |   | A |   | B |   |
    // | H |   | X |   | I |
    // |   | C |   | D |   |
    // | J |   | K |   | L |

    // the center is present in 4 bilinear fetches that all account for .125 of the final image. So he account for .03125 * 4 = .125
    vec3 X = textureLod(source, uv, mipLevel - 1).xyz;
    // central bilinear, weight = .125
    vec3 A = textureLod(source, uv + vec2(-texelSize.x, -texelSize.y), mipLevel - 1).xyz;
    vec3 B = textureLod(source, uv + vec2( texelSize.x, -texelSize.y), mipLevel - 1).xyz;
    vec3 C = textureLod(source, uv + vec2(-texelSize.x,  texelSize.y), mipLevel - 1).xyz;
    vec3 D = textureLod(source, uv + vec2( texelSize.x,  texelSize.y), mipLevel - 1).xyz;
    // sampled once, weight = .125 / 4
    vec3 E = textureLod(source, uv + vec2(-texSize2.x, -texSize2.y), mipLevel - 1).xyz;
    vec3 G = textureLod(source, uv + vec2( texSize2.x, -texSize2.y), mipLevel - 1).xyz;
    vec3 J = textureLod(source, uv + vec2(-texSize2.x,  texSize2.y), mipLevel - 1).xyz;
    vec3 L = textureLod(source, uv + vec2( texSize2.x,  texSize2.y), mipLevel - 1).xyz;
    // sampled twice, weight = .125 / 2
    vec3 F = textureLod(source, uv + vec2(0, -texSize2.y), mipLevel - 1).xyz;
    vec3 H = textureLod(source, uv + vec2( texSize2.x, 0), mipLevel - 1).xyz;
    vec3 I = textureLod(source, uv + vec2( -texSize2.x, 0), mipLevel - 1).xyz;
    vec3 K = textureLod(source, uv + vec2(0, texSize2.y), mipLevel - 1).xyz;

    vec3 downsampled = (X + A + B + C + D) * .125f + (E + G + J + L) * .03125f + (F + H + I + K) * .0625f;
    imageStore(result, texel, vec4(downsampled, 1));
}
