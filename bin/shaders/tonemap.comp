#version 460

layout(rgba16f, binding = 0) readonly uniform image2D imgIn;
layout(rgba8, binding = 1) writeonly uniform image2D imgOut;
uniform sampler2D bloomInput;

uniform int technique;
uniform float gamma;

vec3 GammaCorrect(vec3 color)
{
    return pow(color, vec3(1.f / gamma));
}

float A = 0.15;
float B = 0.50;
float C = 0.10;
float D = 0.20;
float E = 0.02;
float F = 0.30;
float W = 11.2;
vec3 Uncharted2Tonemap(vec3 x)
{
    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

// Thanks http://filmicworlds.com/blog/filmic-tonemapping-operators/
vec3 Uncharted2(vec3 color)
{
    float exposureBias = 2.f;

    vec3 current = Uncharted2Tonemap(exposureBias * color);

    vec3 whiteScale = 1.f / Uncharted2Tonemap(vec3(W));
    vec3 tonemapped = current * whiteScale;
    return GammaCorrect(tonemapped);
}

vec3 ACES(vec3 v)
{
    v *= .6f;
    float a = 2.51f;
    float b = 0.03f;
    float c = 2.43f;
    float d = 0.59f;
    float e = 0.14f;
    return clamp((v*(a*v+b))/(v*(c*v+d)+e), .0f, 1.f);
}

vec3 Reinhard(vec3 color)
{
    vec3 mapped = color / (color + 1.f);
    return GammaCorrect(mapped);
}

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = pixel / vec2(imageSize(imgIn));

    vec3 pixelColor = imageLoad(imgIn, pixel).xyz;

    vec3 mapped;
    switch(technique)
    {
        case 0:
            mapped = Reinhard(pixelColor);
            break;
        case 1:
            mapped = ACES(pixelColor);
            break;
        case 2:
            mapped = Uncharted2(pixelColor);
            break;
        default:
            mapped = pixelColor;
    }

    imageStore(imgOut, pixel, vec4(mapped, 1));
}
